import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		google()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
	}
	dependencies {
		classpath "com.android.tools.build:gradle:$androidPluginVersion"
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
		classpath 'com.badlogicgames.gdx:gdx-tools:1.11.0'
		classpath "space.earlygrey:shapedrawer:$shapedrawerVersion"
		classpath files("libs/libgdx-inGameConsole-1.0.0.jar")
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

configure(subprojects - project(':android')) {
	apply plugin: 'java-library'
	sourceCompatibility = 16.0
	compileJava {
		options.incremental = true
	}

	task packTextures {
		def inputDir = "$rootDir/core/assets/sprites"
		def outputDir = "$rootDir/assets/packaged"
		inputs.dir inputDir
		outputs.dir outputDir
		doLast {
			delete(outputDir)
			TexturePacker.process(inputDir, outputDir, "sasza_clone.pack")
		}
	}

	task packPrivateTextures {
		def inputDir = "$rootDir/core/assets/sasza_clone_private_assets/sprites2"
		def inputDir2 = "$rootDir/core/assets/sasza_clone_private_assets/sprites"
		def outputDir = "$rootDir/assets/packagedPrivate"
		inputs.dir inputDir
		inputs.dir inputDir2
		outputs.dir outputDir
		doLast {
			delete(outputDir)
			TexturePacker.process(inputDir, outputDir, "sasza_clone_private.atlas")
			TexturePacker.process(inputDir2, outputDir, "sasza_clone_private_weapons.atlas")
		}
	}

	task copyLevels(type: Copy){
		from "$rootDir/sasza_clone_levels/scenes"
		into "$rootDir/assets/levels"
		exclude "MainScene.dt"
	}

	project.tasks.compileJava.dependsOn packTextures
	project.tasks.compileJava.dependsOn packPrivateTextures
	project.tasks.compileJava.dependsOn copyLevels
}

subprojects {
	version = '0.0.1'
	ext.appName = 'sasza_clone'
	repositories {
		mavenLocal()
		mavenCentral()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
	}
}

eclipse.project.name = 'sasza_clone' + '-parent'
